﻿Shader "Custom/Terrain" {
	Properties {
		_Color ("Color", Color) = (1,1,1,1)
		_MainTex ("Terrain texture array", 2DArray) = "white" {}
		_GridTex ("Grid texture", 2D) = "white" {}
		_Glossiness ("Smoothness", Range(0,1)) = 0.5
		_Specular ("Specular", Color) = (.2, .2, .2)
		_BackgroundColor("_BackgroundColor", Color) = (0, 0, 0)
		[Toggle(SHOW_MAP_DATA)] _ShowMapData ("Show map data", Float) = 0
	}
	SubShader {
		Tags { "RenderType"="Opaque" }
		LOD 200
		
		CGPROGRAM
		#pragma surface surf StandardSpecular fullforwardshadows vertex:vert
		#pragma target 3.5
		
		#pragma multi_compile _ GRID_ON
		#pragma multi_compile _ HEX_MAP_EDIT_MODE
		
		#pragma shader_feature SHOW_MAP_DATA

		#include "HexMetrics.cginc"
		#include "HexCellData.cginc"

		UNITY_DECLARE_TEX2DARRAY(_MainTex);
		half _Glossiness;
		fixed3 _Specular;
		fixed4 _Color;
		sampler2D _GridTex;
		half3 _BackgroundColor;

		struct Input {
			float4 color : COLOR;
			float3 worldPos;
			float3 terrain;
			float4 visibility;
			
			#if defined(SHOW_MAP_DATA)
				float mapData;
			#endif
		};

		void vert (inout appdata_full v, out Input data) {
			UNITY_INITIALIZE_OUTPUT(Input, data);
			
			float4 cell0 = GetCellData(v, 0);
			float4 cell1 = GetCellData(v, 1);
			float4 cell2 = GetCellData(v, 2);
			
			data.terrain.x = cell0.w;
			data.terrain.y = cell1.w;
			data.terrain.z = cell2.w;
			
			data.visibility.x = cell0.x;
			data.visibility.y = cell1.x;
			data.visibility.z = cell2.x;
			data.visibility.xyz = lerp(.25, 1, data.visibility.xyz);
			data.visibility.w = cell0.y * v.color.x + cell1.y * v.color.y + cell2.y * v.color.z;
			
			#if defined(SHOW_MAP_DATA)
				data.mapData = cell0.z * v.color.x + cell1.z * v.color.y + cell2.z * v.color.z;
			#endif
		}
		
		float4 getTerrainColor (Input IN, int index) {
			float3 uvw = float3(IN.worldPos.xz * (2 * TILING_SCALE), IN.terrain[index]);
			float4 c = UNITY_SAMPLE_TEX2DARRAY(_MainTex, uvw);
			return c * (IN.color[index] * IN.visibility[index]);
		}

		void surf (Input IN, inout SurfaceOutputStandardSpecular o) {
			fixed4 c = 
				getTerrainColor(IN, 0) +
				getTerrainColor(IN, 1) +
				getTerrainColor(IN, 2);
			
			fixed4 grid = 1;
			#if defined(GRID_ON)
				float2 gridUV = IN.worldPos.xz;
				gridUV.x *= 1 / (4 * 8.66025404);
				gridUV.y *= 1 / (2 * 15.0);
				grid = tex2D(_GridTex, gridUV);
			#endif
			
			float explored = IN.visibility.w;
			o.Albedo = c.rgb * grid * _Color * explored;
			#if defined(SHOW_MAP_DATA)
				o.Albedo = IN.mapData * grid;
			#endif
			o.Specular = _Specular * explored;
			o.Smoothness = _Glossiness;
			o.Occlusion = explored;
			o.Emission = _BackgroundColor * (1 - explored);
			o.Alpha = c.a;
		}
		ENDCG
	}
	FallBack "Diffuse"
}